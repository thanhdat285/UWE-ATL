module Requirement2Content;

create OUT: Content from IN: Requirement;

helper def : firstToLower(name : String) : String =
	name.substring(1,1).toLower() + name.substring(2, name.size());

lazy rule Parameter {
	from
		outputPin : Requirement!OutputPin
	to 
		param : Content!Param (
			name <- outputPin.name,
			type <- outputPin.type
		)
}

lazy rule UserAction2Operation {
	from s: Requirement!UserAction 
	to t: Content!Operation (
		name <- thisModule.firstToLower(s.name),
		return <- 'SUCCESS',
		isPublic <- true,
		isStatic <- false
	)
}

rule Activity2ContentClass {
	from s: Requirement!Activity 
	to t: Content!ContentClass (
		name <- s.name,
		operations <- s.nodes->select(node |node.oclIsTypeOf(Requirement!UserAction))->iterate(e; res : Set(Requirement!UserAction) = Set {} |
			if (res->collect(f | f.name)->includes(e.name)) then
				res
			else
				res->including(e)
			endif
			)->collect(e | thisModule.UserAction2Operation(e))
	)
}

